buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

apply plugin: 'android'


dependencies {
    // Add the support lib that is appropriate for SDK 8
    compile "com.android.support:support-v4:19.1.+"
    compile "com.android.support:gridlayout-v7:19.1.+"
}

// The sample build uses multiple directories to
// keep boilerplate and common code separate from
// the main sample code.
List<String> dirs = [
        'main',     // main sample code; look here for the interesting stuff.
        'common',   // components that are reused by multiple samples
        'template'] // boilerplate code that is generated by the sample template process




android {
    compileSdkVersion 19

    buildToolsVersion "19.0.1"

    signingConfigs{
        release{
            storeFile null
            storePassword "none"
            keyAlias "none"
            keyPassword "none"
        }
    }

    if(project.hasProperty("Signing.properties") &&
            new File(project.property("Signing.properties")).exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("Signing.properties"))))
        println ("Got signing properties!")

        signingConfigs {
            release {
                storeFile file(props['storeFile'])
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    } else {
        println ("No signing properties available..")
    }

    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.release
            zipAlign true
        }
    }
    sourceSets {
        main {
            dirs.each { dir ->
                java.srcDirs "src/${dir}/java"
                res.srcDirs "src/${dir}/res"
            }
        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']

    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
